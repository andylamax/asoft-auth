buildscript {
    ext {
        kotlin_version = '1.3.40'
        serialization_version = "0.11.1"
        klock_version = "1.5.0"
        asoft_neo4j_version = "1.0.1"
        persist_version = "2.2.0"
    }

    repositories {
        google()
        jcenter()
    }
    
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.40'
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("install")) {
//        task.enabled = false
    }
}

group 'tz.co.asoft'
version '3.0.1'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://jitpack.io" }
}


kotlin {
    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    js() {
        browser{
            testTask {
                useKarma {
                    useChrome()
                }
            }
        }
        nodejs()
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                kotlinOptions.metaInfo = true
                kotlinOptions.sourceMap = true
                kotlinOptions.moduleKind = 'commonjs'
                kotlinOptions.main = "call"
            }
        }
    }

    android {
        publishLibraryVariants("release")
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
                api "com.soywiz.korlibs.klock:klock-metadata:$klock_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-metadata:$persist_version"
                api "com.github.andylamax.asoft-neo4j:asoft-neo4j-metadata:$asoft_neo4j_version"
            }
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmCommonMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
            }
        }

        jvmCommonTest {
            dependsOn jvmCommonMain
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        jvmMain {
            dependsOn jvmCommonMain
            dependencies {
                api "com.soywiz.korlibs.klock:klock-jvm:$klock_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-jvm:$persist_version"
                api "com.github.andylamax.asoft-neo4j:asoft-neo4j-jvm:$asoft_neo4j_version"
            }
        }

        jvmTest {
            dependsOn jvmMain
            dependsOn jvmCommonTest
        }

        androidMain {
            dependsOn jvmCommonMain
            dependencies {
                api "com.soywiz.korlibs.klock:klock-android:$klock_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-android:$persist_version"
                api "com.github.andylamax.asoft-neo4j:asoft-neo4j-android:$asoft_neo4j_version"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
                api "com.soywiz.korlibs.klock:klock-js:$klock_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-js:$persist_version"
                api "com.github.andylamax.asoft-neo4j:asoft-neo4j-js:$asoft_neo4j_version"
            }
        }

        jsTest {
            dependsOn jsMain
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}